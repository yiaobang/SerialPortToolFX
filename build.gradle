plugins {
    id 'java'
    id('application')
    id('org.javamodularity.moduleplugin') version '2.0.0'
    id('org.openjfx.javafxplugin') version '0.1.0'
    id('org.beryx.jlink') version '3.2.1'
}

group = 'com.yiaobang'
version = '2.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.aliyun.com/repository/public/' }
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

javafx {
    version = '25.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}


dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    implementation 'io.github.mkpaz:atlantafx-base:2.1.0'
    implementation 'com.typesafe:config:1.4.5'
    implementation 'com.fazecast:jSerialComm:2.11.4'
    implementation 'org.apache.commons:commons-text:1.14.0'
    implementation 'commons-codec:commons-codec:1.19.0'
    implementation 'com.google.code.gson:gson:2.13.2'

    testImplementation platform('org.junit:junit-bom:5.12.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

//test {
//    useJUnitPlatform()
//}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    applicationName = 'SerialPortToolFX'
    mainClass = 'com.yiaobang.serialporttoolfx.AppLauncher'
    mainModule = 'SerialPortToolFX'
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8'
                                 , '-XX:+UseZGC'
                                 , '-XX:+UseCompactObjectHeaders'
                                 , '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED'
                                 , '--add-opens=java.base/java.io=ALL-UNNAMED'
                                 , '--enable-native-access=javafx.graphics,com.fazecast.jSerialComm'
    ]
}
run {
    jvmArgs += [
            "--enable-native-access=javafx.graphics,com.fazecast.jSerialComm"
    ]
}
jlink {
    options.set(List.of('--strip-debug', '--compress', 'zip-9', '--no-header-files', '--no-man-pages'))
    launcher {
        name = "SerialPortToolFX"
        //imageName.set("SerialPortToolFX")
        jvmArgs = [
                '--Dfile.encoding=UTF-8',
                '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
                '--add-opens=java.base/java.io=ALL-UNNAMED',
                // 必须授权，否则 jSerialComm 无法加载本地库
                '--enable-native-access=javafx.graphics,com.fazecast.jSerialComm'
        ]
    }
    imageZip.set(project.layout.buildDirectory.file("image-zip/${application.applicationName}-${version}-Portable.zip"))

    jpackage {
        outputDir = 'jpackage'
        imageName = "SerialPortToolFX"       // 镜像文件夹名字带版本号
        installerName = "SerialPortToolFX"   // 安装包文件名带版本号
        appVersion = version                               // 避免 jpackage 再自动拼一次
        vendor = "yiaobang"
        description = "A practical serial port utility built with JavaFX."
        jvmArgs = [
                '--enable-native-access=javafx.graphics,com.fazecast.jSerialComm'
        ]
        // 是否跳过创建安装包（可根据需求设为 true/false）
        skipInstaller = false

        // --- 平台特定配置 ---
        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.isWindows()) {
            icon = 'src/main/resources/application.ico'
            installerOptions = [
                    '--win-dir-chooser',           // 允许用户选择安装目录
                    '--win-menu',                  // 在开始菜单创建快捷方式
                    '--win-shortcut',              // 在桌面创建快捷方式
                    '--win-menu-group', application.applicationName, // 菜单组名
                    '--win-upgrade-uuid', 'c1f5e3a8-6f4a-4c2a-9c95-8d3c7345b91e', // 升级唯一标识
                    '--win-shortcut-prompt',       // 提示是否创建快捷方式
                    '--win-help-url', 'https://github.com/yiaobang/SerialPortToolFX' // 帮助链接
            ]
        }
        if (os.isMacOsX()) {
            icon = 'src/main/resources/ico.icns'
            installerOptions = [
                    '--mac-package-identifier', 'com.yiaobang.serialPortToolFX' // 应用包标识符
            ]
        }
        if (os.isLinux()) {
            icon = 'src/main/resources/ico.png'
            installerType = 'deb'
            installerOptions=[
                    '--linux-deb-maintainer', 'yiaobang@gmail.com', // 维护者信息
                    '--linux-menu-group', application.applicationName, // 菜单组名
                    '--linux-shortcut', // 创建桌面快捷方式
                    '--linux-app-category', 'Utility', // 应用类别
                    '--linux-description', 'A practical serial port utility built with JavaFX.', // 应用描述
                    '--linux-url', 'https://github.com/yiaobang/SerialPortToolFX', // 应用主页
                    '--linux-bundle-name', 'serialPortToolFX' // 安装包名
            ]
        }

    }
}